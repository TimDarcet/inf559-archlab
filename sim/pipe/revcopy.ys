#/* $begin revcopy-ys */
##################################################################
# revcopy.ys
#
# Copy a src block of len words to dst in reverse order.
# Return the number of positive words (>0) contained in src.
#
# Timothee Darcet timothee.darcet
#
# Use iaddq to use less registers
# Reformulate conditional jumps to be taken as often as possible (since the pipe simulator supposes they are taken)
#
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# %rbx: tmp storage
# %rax: count
# You can modify this portion
		# Loop header
		addq %rdx, %rdx			# %rdx = 2 * len
		jg Next			    	# if > 0 , skip ret
		ret
Next: 	addq %rdx, %rdx			# %rdx = 4 * len
		irmovq $0, %rax			# count = 0;
		addq %rdx, %rdx			# %rdx = 8 * len
		iaddq $-8, %rsi			# Adjust offset
		addq %rdx, %rsi			# %rsi is at the end of word
Loop:	mrmovq (%rdi), %rbx		# read val from src...
		iaddq $8, %rdi			# src++
		rmmovq %rbx, (%rsi)		# ...and store it to dst
		iaddq $-8, %rsi			# dst--
		andq %rbx, %rbx			# val <= 0?
		jg pos
		jmp Npos					# if so, goto Npos: (skip count++)
pos:	iaddq $1, %rax			# count++
Npos:	iaddq $-8, %rdx			# len--
		jg Loop					# if there are still things to copy, goto Loop:
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end revcopy-ys */
